<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%">
	
	<fx:Metadata>
		[Event(name="speakerChange", type="au.com.cfobjective.events.SpeakerEvent")]		
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import au.com.cfobjective.events.SessionEvent;
			import au.com.cfobjective.events.SpeakerEvent;
			import au.com.cfobjective.model.Speaker;
			
			import mx.collections.ArrayCollection;
			
			[Bindable] public var speakers:ArrayCollection;
			[Bindable] protected var searchFor:String = '';
			
			protected function changeSpeaker(speaker:Speaker):void {
				var event:SpeakerEvent = new SpeakerEvent(SpeakerEvent.CHANGE, speaker);
				
				dispatchEvent(event);
			}
			
			protected function filterList(item:Object):Boolean {
				var speaker:Speaker = (item as Speaker);
				var filter:String = searchFor.toLowerCase();
				var name:String = speaker.name.toLowerCase();
				var twitter:String = speaker.twitter.toLowerCase();
				var bio:String = speaker.bio.toLowerCase();
				
				if (filter == '') {
					return true;
				}
				
				if (name.search(filter) >= 0 || bio.search(filter) >= 0 || twitter.search(filter) >= 0) { 
					return true;
				}
				
				return false;
			}
			
			protected function filterSessions(text:String):void {
				searchFor = text;
				speakers.filterFunction = filterList;
				speakers.refresh();
			}
		]]>
	</fx:Script>
	
	<s:TextInput id="search" width="100%" text="{searchFor}"
				 skinClass="com.pialabs.eskimo.skins.mobile.ios.SearchTextInputSkin"
				 prompt="Search" paddingLeft="10" paddingRight="10" change="filterSessions(search.text)" />
	<s:List id="speakerList" dataProvider="{speakers}" labelField="name" change="changeSpeaker(speakerList.selectedItem)" height="100%" width="100%" />
</s:VGroup>
