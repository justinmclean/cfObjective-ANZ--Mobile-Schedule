<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%" initialize="setup()" xmlns:ios="com.pialabs.eskimo.skins.mobile.ios.*">
	
	<fx:Metadata>
		[Event(name="sessionChange", type="au.com.cfobjective.events.SessionEvent")]		
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import au.com.cfobjective.events.SessionEvent;
			import au.com.cfobjective.model.Session;
			import au.com.cfobjective.model.Speaker;
			import au.com.cfobjective.views.SessionsView;
			
			import mx.collections.ArrayCollection;
			
			import spark.components.NavigatorContent;
			import spark.components.ViewNavigator;
			
			[Bindable] public var sessions:ArrayCollection;
			[Bindable] protected var searchFor:String = '';
			
			protected function setup():void {
				filterSessions(searchFor);
			}
			
			protected function changeSession(session:Session):void {
				var event:SessionEvent = new SessionEvent(SessionEvent.CHANGE, session);
				
				dispatchEvent(event);
			}
			
			protected function filterList(item:Object):Boolean {
				var session:Session = (item as Session);
				var filter:String = searchFor.toLowerCase();
				var title:String = session.title.toLowerCase();
				var description:String = session.description.toLowerCase();
				var names:Array = [];
				var stream:String = session.stream.toLowerCase();
	
				for each (var speaker:Speaker in session.speakers) {
					names.push(speaker.name.toLowerCase());
				}
				
				if (filter == '') {
					return true;
				}
				
				if (title.search(filter) >= 0 || description.search(filter) >= 0 || stream.search(filter) >= 0) { 
					return true;
				}
				
				for each (var name:String in names) {
					if (name.search(filter) >= 0) {
						return true;
					}
				}
				
				return false;
			}
			
			protected function filterSessions(text:String):void {
				searchFor = text;
				sessions.filterFunction = filterList;
				sessions.refresh();
			}
		]]>
	</fx:Script>
	<s:TextInput id="search" width="100%" text="{searchFor}"
				 skinClass="com.pialabs.eskimo.skins.mobile.ios.SearchTextInputSkin"
				 prompt="Search" paddingLeft="10" paddingRight="10" change="filterSessions(search.text)" />
	<s:List id="sessionList" dataProvider="{sessions}" labelField="title" change="changeSession(sessionList.selectedItem as Session)" height="100%" width="100%" />
</s:VGroup>
